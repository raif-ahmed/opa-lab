apiVersion: templates.gatekeeper.sh/v1beta1
kind: ConstraintTemplate
metadata:
  creationTimestamp: null
  name: containerdenynotallowedlabels
spec:
  crd:
    spec:
      names:
        kind: ContainerDenyNotAllowedLabels
      validation:
        openAPIV3Schema:
          properties:
            labels:
              description: Array of required label keys.
              items:
                type: object
              type: array
  targets:
  - libs:
    - |-
      package lib.common

      default is_gatekeeper = false

      common_labels := [
        {"key": "app.kubernetes.io/name", "allowedRegex": "*"},
        {"key": "app.kubernetes.io/instance", "allowedRegex": "*"},
        {"key": "app.kubernetes.io/version", "allowedRegex": "*"},
        {"key": "app.kubernetes.io/component", "allowedRegex": "*"},
        {"key": "app.kubernetes.io/part-of", "allowedRegex": "*"},
        {"key": "app.kubernetes.io/managed-by", "allowedRegex": "*"},
        {"key": "objectset.rio.cattle.io/hash", "allowedRegex": "*"},
        {"key": "kubernetes.io/metadata.name", "allowedRegex": "*"},
      ]

      has_field(obj, field) {
        not object.get(obj, field, "N_DEFINED") == "N_DEFINED"
      }

      missing_field(obj, field) {
        obj[field] == ""
      }

      missing_field(obj, field) {
        not has_field(obj, field)
      }

      is_gatekeeper {
        has_field(input, "review")
        has_field(input.review, "object")
      }

      is_combined {
        has_field(input[_], "contents")
      }

      resource = input.review.object {
        is_gatekeeper
      }

      resource = input {
        not is_gatekeeper
        not is_combined
      }

      resource = input[0].contents {
        not is_gatekeeper
        is_combined
      }

      parameters = input[1].contents.parameters {
        not is_gatekeeper
        is_combined
      }

      parameters = input.parameters {
        is_gatekeeper
      }

      inventory = input[1].contents.inventory {
        not is_gatekeeper
        is_combined
      }

      inventory = data.inventory {
        is_gatekeeper
      }

      input_containers[c] {
        c := resource.spec.containers[_]
      }

      input_containers[c] {
        c := resource.spec.initContainers[_]
      }

      input_containers[c] {
        c := resource.spec.template.spec.containers[_]
      }

      input_containers[c] {
        c := resource.spec.template.spec.initContainers[_]
      }

      input_volumes[v] {
        v := resource.spec.volumes[_]
      }

      input_volumes[v] {
        v := resource.spec.template.spec.volumes[_]
      }

      contains_label(allowedLabels, key, value) {
        allowedLabels[_].key == key

        # do not match if any allowedRegex is not defined, or is an empty string
        allowedLabels[_].allowedRegex != ""
        re_match(allowedLabels[_].allowedRegex, value)
      } else = false

      not_contains_label(labels, key, value) {
        not contains_label(labels, key, value)
      }

      is_common_label(key, value) {
        # as of now it is just equality but in future it can be regex
        common_labels[_].key == key
      } else = false

      not_common_label(key, value) {
        not is_common_label(key, value)
      }

      contains_prefix(allowedPrefixes, value) {
        # do not match if any allowedRegex is not defined, or is an empty string
        allowedPrefixes[_] != ""
        startswith(value, allowedPrefixes[_])
      } else = false

      not_contains_prefix(allowedPrefixes, value) {
        not contains_prefix(allowedPrefixes, value)
      }

      contains_array_elem(array, elem) {
        array[_] == elem
      } else = false

      not_contains_array_elem(array, elem) {
        not contains_array_elem(array, elem)
      }

      identical_k8s_objects(obj1, obj2) {
        has_field(obj1.metadata, "namespace")
        has_field(obj2.metadata, "namespace")
        obj1.metadata.namespace == obj2.metadata.namespace
        obj1.metadata.name == obj2.metadata.name
        obj1.kind == obj2.kind
        obj1.apiVersion == obj2.apiVersion
      }

      identical_k8s_objects(obj1, obj2) {
        missing_field(obj1.metadata, "namespace")
        missing_field(obj2.metadata, "namespace")
        obj1.metadata.name == obj2.metadata.name
        obj1.kind == obj2.kind
        obj1.apiVersion == obj2.apiVersion
      }
    rego: |-
      package container_deny_not_allowed_labels

      import data.lib.common as common_lib

      allowedLabels := common_lib.parameters.labels

      definedLabels := common_lib.resource.metadata.labels

      violation[{"msg": msg, "details": {}}] {
        some key
        value := definedLabels[key]
        common_lib.not_contains_label(allowedLabels, key, value)
        common_lib.not_common_label(key, value)
        msg := sprintf("Label <%v: %v> does not satisfy allowed regex", [key, value])
      }
    target: admission.k8s.gatekeeper.sh
status: {}
